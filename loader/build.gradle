apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "me.piebridge.brevent.loader"
        minSdkVersion 23
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile project(':log-reader')
}

def hiddenApi = project(':hidden-api').file('build/intermediates/bundles/release/classes.jar')

if (project.gradle.startParameter.taskRequests?.empty) {
    // for ide, aar cannot be provided
    dependencies {
        compile project(':hidden-api')
    }
} else {
    // for proguard
    dependencies {
        provided files(hiddenApi)
    }
}

tasks.withType(JavaCompile) {
    doFirst {
        options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
        options.compilerArgs << '-Xbootclasspath/p:' + hiddenApi
    }
}

tasks.whenTaskAdded { task ->
    if (task.name == 'prepareDebugDependencies' || task.name == 'prepareReleaseDependencies') {
        task.finalizedBy ':hidden-api:bundleRelease'
    }
}
